apply plugin: 'java'
apply plugin: 'eclipse-wtp'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {
  compile 'com.github.gameontext:signed:v1.0.2'
  testCompile group: 'junit', name: 'junit', version: '4.+'
  testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
  testCompile group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.16.0'
  testCompile group: 'org.mockito', name: 'mockito-core', version: '1.+'
  testCompile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.22.2'
}

test {
  testLogging {
    showStandardStreams = false
  }
}

eclipse.wtp.facet {
  // Clear default facets to work around bug where you get duplicates including wst 2.4
  facets = []
  facet name: 'jst.java', type: 'fixed'
}

// Add additional code conventions
eclipse.jdt.file {
  withProperties {
    it['org.eclipse.jdt.core.formatter.indentation.size'] = '4'
    it['org.eclipse.jdt.core.formatter.tabulation.size'] = '4'
    it['org.eclipse.jdt.core.formatter.tabulation.char'] = 'space'
    it['org.eclipse.jdt.core.formatter.indent_switchstatements_compare_to_cases'] = 'true'
    it['org.eclipse.jdt.core.formatter.indent_switchstatements_compare_to_switch'] = 'true'
  }
}

// This is the default build and will only provide classes in this project in the final jar
jar {
  manifest {
    attributes 'Main-Class': 'org.gameontext.util.reg.RegistrationUtility'
  }
}


// Use this task when you need a fat jar that contains all the required classes as well
task fatjar (type: Jar) {
  from {
    (configurations.runtime).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  from sourceSets.main.runtimeClasspath
  manifest {
    attributes 'Main-Class': 'org.gameontext.util.reg.RegistrationUtility'
  }
}
